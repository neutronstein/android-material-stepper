apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply from: "$rootDir.path/config/project_properties.gradle"
apply from: '../sample/code_coverage.gradle'

android {
    compileSdkVersion project.androidCompileSdkVersion
    buildToolsVersion("$androidBuildToolsVersion")

    defaultConfig {
        applicationId "com.stepstone.stepper.sample"
        minSdkVersion project.androidMinSdkVersion
        targetSdkVersion project.androidTargetSdkVersion
        versionCode 1
        versionName "1.0"

        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "com.stepstone.stepper.sample.test.runner.TestButlerRunner"
        com.android.ddmlib.DdmPreferences.setTimeOut(60000)
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            testCoverageEnabled true
        }
    }

    testOptions {
        animationsDisabled true
    }

    adbOptions {
        installOptions getAdbInstallOptions()
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

    implementation project(':material-stepper')
    implementation "androidx.appcompat:appcompat:1.0.0"
    implementation "androidx.recyclerview:recyclerview:1.0.0"
    implementation "com.google.android.material:material:1.0.0"

    implementation "com.jakewharton:butterknife:$butterknifeVersion"
    kapt "com.jakewharton:butterknife-compiler:$butterknifeVersion"

    implementation "uk.co.chrisjenx:calligraphy:$calligraphyVersion"

    androidTestImplementation project(':espresso-material-stepper')

    androidTestImplementation "androidx.test:runner:1.1.0"
    androidTestImplementation "androidx.test:rules:1.1.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.1.0"
    androidTestImplementation "androidx.test.espresso:espresso-intents:3.1.0"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:3.1.0"
    androidTestImplementation "com.linkedin.testbutler:test-butler-library:$testButlerVersion"

    androidTestUtil "com.linkedin.testbutler:test-butler-app:$testButlerVersion@apk"
    androidTestImplementation("androidx.annotation:annotation:1.0.0") {
        force = true
    }
    androidTestImplementation("androidx.legacy:legacy-support-v4:1.0.0") {
        force = true
    }
    androidTestImplementation("androidx.appcompat:appcompat:1.0.0") {
        force = true
    }
}

def instrumentationTestTaskName = 'connectedCheck'

apply from: '../sample/screenshots.gradle'

tasks[instrumentationTestTaskName].finalizedBy {
    [fetchScreenshotsTask, generateScreenshotReportTask]
}

def findTaskInLib(String taskName) {
    return rootProject.childProjects['material-stepper'].getTasksByName(taskName, false).first()
}

task ('checkWithUiTests', dependsOn: [generateScreenshotReportTask, findTaskInLib('testDebugUnitTest'), findTaskInLib('lintDebug'), tasks['mergedJacocoTestReport'], findTaskInLib('pmd'), findTaskInLib('findbugs'), findTaskInLib('checkstyle')]) {
    group = "Verification"
    description = "Runs unit and UI tests with code coverage and makes code analysis."
}
